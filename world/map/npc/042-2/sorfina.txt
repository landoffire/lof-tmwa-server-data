// This file is part of the Tutorial
// Author: Jenalya

042-2,28,26,0|script|Sorfina|154,4,4
{
    callfunc "ClearVariables";

    if (FLAGS & FLAG_TUTORIAL_DONE) goto L_HatCheck;
    if (QL_SORFINA == 4) goto L_Heal;
    if (QL_SORFINA == 3) goto L_Clothes;
    if (QL_SORFINA == 2) goto L_Chest;
    if (QL_SORFINA == 1) goto L_Carpet;

    callfunc "GameRules";
    goto L_Start;

L_Start:
    mes "You open your eyes.";
    mes "";
    mes "[Old Woman]";
    mes "\"Ah, you woke up. It seems you collapsed after you left the boat that brought you to Tulimshar.";
    mes "A group of ragtags immediately took advantage of that and stole all your stuff!\"";
    next;
    mes "\"My little granddaughter Tanisha saw that, but couldn't do anything to stop them.";
    mes "She hurried to get someone to help. It was too late to catch the thieves, but at least the helpers brought you here to recover.\"";
    next;
    mes "\"We're in the suburbs of Tulimshar here and this area of the town is full of shady people, so I don't think there's a chance to get your stuff back.\"";
    mes "She has a sad look on her face, but then she shakes her head and smiles.";
    next;
    mes "[Old Woman]";
    mes "\"Did you come here to look for a job? Many young people come to Tulimshar for this reason.\"";
    next;
    mes "\"I can tell you some useful things, if you want to hear.";
    mes "Oh, and by the way my name is Sorfina.\"";
    next;
    mes "[Server]";
    mes "The explanations in this tutorial are based on the official client, 0.5 series.";
    mes "If you're using another client, settings and controls may be different.";
    next;
    menu
        "That would be great!",L_Tut,
        "No, thanks. I already understand what's going on.", L_SkipTut;

L_Tut:
    mes "She smiles at you.";
    mes "[Sorfina]";
    mes "\"A wise decision.\"";
    next;
    mes "\"You still look rather shaky.";
    mes "You're not used to the hot weather, right?\"";
    next;
    goto L_Carpet;

L_Carpet:
    mes "[Sorfina]";
    mes "\"Try to take a few steps and walk to that red carpet over there.";
    mes "Just use the arrow keys or click on the place you want to go to.\"";
    if (!QL_SORFINA)
        set QL_SORFINA, 1;
    goto L_Close;

L_Chest:
    mes "[Sorfina]";
    mes "\"In that chest there are some clothes you can take.\"";
    next;
    mes "\"To interact with things in your environment or talk to NPCs you can either click on it or press N to focus and T to talk/activate.\"";
    next;
    mes "\"Now get the clothes out of the chest.\"";
    goto L_Close;

L_Naked:
    mes "[Sorfina]";
    mes "\"Press F3 or click on the Inventory button in the bar at the upper right corner to open your bag.\"";
    next;
    mes "\"Once we stop talking, click on the clothes and press the equip button.\"";
    next;
    mes "\"Talk to me again after you get dressed. You can either click on me, or press N to focus on me and T to talk.\"";
    goto L_Close;

L_Clothes:
    if (getequipid(equip_torso) < 0)
        goto L_Naked;
    mes "[Sorfina]";
    mes "\"That's much better, don't you think?\"";
    next;
    mes "\"I think this will help you a bit. To get more information, press the button Setup and look at the controls on the Keyboard tab. You can also change them as you like.";
    mes "Talk to me again if you want me to explain more things.\"";
    next;
    mes "\"Now you should go to see my granddaughter, she is very worried. She's in the storehouse, that's the second building southeast from here.\"";
    next;
    mes "\"Oh, and if you're looking for work, you should talk to Aidan when you go to Tulimshar. They're giving rewards for monster hunting.";
    mes "You can find him near the south gate.\"";
    next;
    mes "She hesitates a moment.";
    mes "\"[Sorfina]\"";
    mes "\"And here is some money for you. At least you can buy yourself something to eat.";
    mes "Good luck!\"";
    set Zeny, Zeny + 30;
    set QL_SORFINA, 4;
    goto L_Close;

L_Heal:
    if (Hp == MaxHp)
        goto L_Again;
    mes "[Sorfina]";
    mes "\"Hello Dear! You look tired, you should rest a moment.\"";
    mes "You take a short nap and feel refreshed.";
    heal MaxHp, 0;
    next;
    goto L_Again;

L_HatCheck:
    set @hat$, "";

    if (Sorfina_Caps == 9 && BaseLevel >= 99) set @hat$, "Level99Cap";
    if (Sorfina_Caps == 8 && BaseLevel >= 90) set @hat$, "Level90Cap";
    if (Sorfina_Caps == 7 && BaseLevel >= 80) set @hat$, "Level80Cap";
    if (Sorfina_Caps == 6 && BaseLevel >= 70) set @hat$, "Level70Cap";
    if (Sorfina_Caps == 5 && BaseLevel >= 60) set @hat$, "Level60Cap";
    if (Sorfina_Caps == 4 && BaseLevel >= 50) set @hat$, "Level50Cap";
    if (Sorfina_Caps == 3 && BaseLevel >= 40) set @hat$, "Level40Cap";
    if (Sorfina_Caps == 2 && BaseLevel >= 30) set @hat$, "Level30Cap";
    if (Sorfina_Caps == 1 && BaseLevel >= 20) set @hat$, "Level20Cap";
    if (Sorfina_Caps == 0 && BaseLevel >= 10) set @hat$, "Level10Cap";

    if (@hat$ == "") goto L_Again;

    getinventorylist;
    if (@inventorylist_count == 100)
        goto L_NoRoom;

    setarray @message_list$,
        "I've been keeping it a secret, but I've finally finished a present for you: an emblazoned cap. It's a celebration of what you've achieved so far. Come back later and I'll have another cap for you.",
        "Ah, here's the next cap. Keep up the good work!",
        "You are doing well! Take this cap and I'll see you soon!",
        "I've finished you another cap, and I'm proud of your progress. Never stop improving!",
        "Your endurance is impressive. Here's your cap; I'll be waiting here when you earn the next.",
        "You aren't a household name yet, but soon all Tulimshar will acknowledge your prowess. Take this cap and carry on!",
        "Tanisha tells me that several of her friends hope to be adventurers some day, 'just like " + strcharinfo(0) + ".' Take your cap, and prove their adoration well-placed.",
        "Most adventurers would have settled down by now into the faint twilight of their fame, but you've kept going. I'm proud of you, although I fear it may be some time before the next cap is finished.",
        "Your cap is finished at last, and you have earned it. The world rings with your prowess, but there are yet heights you have not yet achieved...",
        "I would never have thought when you were brought to my house for the first time, weak and ragged, that you would become one of the most powerful adventurers ever to grace the streets of Tulimshar. This is my last cap for you, now go and use your power wisely!";

    mes "[Sorfina]";
    mes "\"" + @message_list$[Sorfina_Caps] + "\"";

    cleararray @message_list$, 10, "";

    getitem @hat$, 1;
    set @hat$, "";
    set Sorfina_Caps, Sorfina_Caps + 1;
    goto L_Close;

L_NoRoom:
    mes "[Sorfina]";
    mes "\"Oh, I have something to give you, but it seems as if you have nowhere to put it. Come back when you are carrying a little less!\"";
    goto L_Close;

L_Again:
    mes "[Sorfina]";
    mes "\"Make yourself at home! Do you have any questions?\"";
    next;
    if (QL_HASAN && QL_HASAS != 4)
        menu
            "A guy named Hasan won't let me enter Tulimshar.", L_Hasan,
            "Yes, could you explain again about...", L_Explain,
            "No thanks.", L_Close;
    menu
        "Yes, could you explain about...", L_Explain,
        "No, thanks.", L_Close;

L_Explain:
    menu
        "... walking?", L_Walk,
        "... talking?", L_Talk,
        "... the inventory and items?", L_Items,
        "... shortcuts?", L_Shortcut,
        "... what to do in Tulimshar?", L_Aidan,
        "... nevermind.", L_Close;

L_Walk:
    mes "[Sorfina]";
    mes "\"You can use the arrow keys to walk around.";
    mes "Or just click on the place you want to go to.";
    mes "But this only works if that place is reachable and walkable.\"";
    goto L_Close;

L_Talk:
    mes "[Sorfina]";
    mes "\"If you want to talk to a normal person, you can click at him or her.";
    mes "Or you can press N to focus on the nearest person and then press T to talk.\"";
    next;
    mes "\"If you want to talk to other adventurers, press Enter to open your chat window.";
    mes "Then type your message and press Enter to send.\"";
    next;
    mes "\"You may want to talk privately to another adventurer sometimes. In that case you can whisper.";
    mes "After pressing Enter type '/whisper [name] [message]'.";
    mes "Or you can right click on someone and choose the whisper option.\"";
    next;
    mes "\"Press F7 to show and hide your chat window.\"";
    goto L_Close;

L_Items:
    mes "[Sorfina]";
    mes "\"There are three types of items.";
    mes "They can be Consumables, Equipment or Miscellaneous.\"";
    next;
    mes "\"Consumable items such as Potions, can be used only once.";
    mes "After use, they will disappear from your inventory.";
    mes "There are some rare items that can be used but don't get consumed.";
    next;
    mes "\"Equipment items like Armors, Weapons, Accessories can be equipped for fashionable purposes or to raise your status.\"";
    next;
    mes "\"Miscellaneous items such as maggot slime, can be used in creating other items, or just to trade and sell.\"";
    next;
    mes "\"Press F3 to open your inventory.";
    mes "When placing your cursor over an item there, you can see a box with some information about that item.\"";
    next;
    mes "\"To use or equip an item, select it by clicking on it.";
    mes "Then press Use or Equip.\"";
    goto L_Close;

L_Shortcut:
    mes "[Sorfina]";
    mes "\"Press F8  or click the Shortcut button in the bar at the upper right to open your shortcut window.\"";
    next;
    mes "\"You can select the item you want to put on a shortcut with your mouse and then click on the position in the shortcut window you want to place it.\"";
    next;
    mes "\"Now you can use or equip/unequip it by pressing the number of the shortcut.\"";
    goto L_Close;

L_Aidan:
    mes "[Sorfina]";
    mes "\"I suggest that you go to the bazaar near the south gate.";
    mes "We're in the north of Tulimshar here, so you have to walk across the town.\"";
    next;
    mes "\"When you leave this area to the Southeast, you'll come to the docks.";
    mes "Follow the street to the south to pass the old town gates.\"";
    next;
    mes "\"Turn west at the next intersection. You'll pass by the gates of Tulimshar's famous magic school.";
    mes "Just follow the street to the south and you'll reach the bazaar.\"";
    next;
    mes "\"I heard Aidan is looking for brave adventurers, you might want to talk to him.";
    mes "Usually the bazaar is bustling with activity, there are many people who can help.";
    mes "In fact, you should talk to everybody. You can never tell when they might know something useful.\"";
    goto L_Close;

L_Hasan:
    mes "[Sorfina]";
    mes "\"Ah, Hasan. He's causing trouble again?\"";
    mes "She shakes her head.";
    next;
    mes "[Sorfina]";
    mes "\This guy has been a plague ever since he could walk. He's always causing trouble.\"";
    next;
    mes "\"And his father... Well, one day his father thought he should teach Hasan a lesson.\"";
    next;
    mes "\"He took him into the desert and then left him there!";
    mes "Hasan was only seven years old!\"";
    next;
    mes "\"I never understood how someone could do something like that to a child, even if it's a rascal like Hasan.\"";
    next;
    mes "\"Anyway, Hasan tried to find his way back home, but stumbled into a nest of scorpions!\"";
    mes "She shakes her head.";
    next;
    mes "\"If Valon hadn't have come along that moment, Hasan would have died that day.\"";
    next;
    mes "\"However, the scorpion's poison caused a bad fever and once Hasan had recovered from that, he was even more malicious than before.\"";
    mes "She sighs.";
    if (QL_HASAN == 1)
        set QL_HASAN, 2;
    goto L_Close;

L_SkipTut:
    mes "[Sorfina]";
    mes "\"Really? You seem quite unexperienced. Listening to some advice won't hurt.\"";
    next;
    menu
        "Thinking about it twice... please tell me.",L_Tut,
        "Yeah, but thanks for the offer. Bye.", L_SkippedTut;

L_SkippedTut:
    mes "[Sorfina]";
    mes "\"Alright, if you say so. But take at least this.";
    callfunc "TutorialCompleted";
    getitem "Knife", 1;
    getitem "SlingShot", 1;
    getitem "SlingBullet", 100;
    getitem "CottonShirt", 1;
    getitem "CottonTrousers", 1;
    set Zeny, Zeny + 50;
    mes "Good luck.\"";
    goto L_Close;

L_Close:
    close;

OnTouch:
    if (TUT_var != 0)
        end;
    callfunc "GameRules";
    goto L_Start;
}

042-2,33,27,0|script|#Carpet|32767,1,1
{
    end;

OnTouch:
    if (QL_SORFINA != 1)
        end;
    mes "[Sorfina]";
    mes "\"Very good! I'm glad you're recovering.";
    mes "Now you should get dressed again.\"";
    next;
    mes "Sorfina unlocks the chest behind her.";
    next;
    mes "[Sorfina]";
    mes "\"In that chest there are some clothes you can take.\"";
    next;
    mes "\"To interact with things in your environment or talk to NPCs you can either click on it or press N to focus and T to talk/activate.\"";
    next;
    mes "\"Now get the clothes out of the chest.\"";
    set QL_SORFINA, 2;
    close;
}

042-2,27,23,0|script|Chest#sorfina|111
{
    if (QL_SORFINA > 2) goto L_Empty;
    if (QL_SORFINA == 2) goto L_Shirt;

    mes "The chest is locked.";
    close;

L_Shirt:
    mes "You take a simple Cotton Shirt and some Cotton Trousers out of the chest.";
    getitem "CottonShirt", 1;
    getitem "CottonTrousers", 1;
    set QL_SORFINA, 3;
    next;
    mes "[Sorfina]";
    mes "\"Great! Now you can equip it.\"";
    next;
    mes "\"You can open your inventory by pressing F3 or clicking on the 'Inventory' button in the bar at the upper right corner.\"";
    next;
    mes "\"After we finish talking, click on the clothes and press the equip button.\"";
    close;

L_Empty:
    mes "The chest is empty.";
    close;
}

042-2,44,31,0|script|#DoorOut-sorfina|45,0,0
{
    end;

OnTouch:
    if ((QL_SORFINA < 4)
        && !(FLAGS & FLAG_TUTORIAL_DONE))
            goto L_Block;
    warp "042-1", 63, 77;
    end;

L_Block:
    message strcharinfo(0), "Sorfina: Wait a moment! You're not ready to leave.";
    end;
}
